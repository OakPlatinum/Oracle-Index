plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // owo lib
    modImplementation "io.wispforest:owo-lib:0.12.11+1.21"

    implementation "org.jsoup:jsoup:1.17.2"
    include "org.jsoup:jsoup:1.17.2"

    implementation 'dev.langchain4j:langchain4j:1.0.0'
    include 'dev.langchain4j:langchain4j:1.0.0'
    implementation 'dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2-q:1.0.0-beta5'
    include 'dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2-q:1.0.0-beta5'

    implementation 'net.objecthunter:exp4j:0.4.8'
    include 'net.objecthunter:exp4j:0.4.8'

    modRuntimeOnly "teamreborn:energy:4.1.0"
    modImplementation "curse.maven:oritech-1030830:6520539" // oritech 0.15.5
    modRuntimeOnly "curse.maven:architectury-api-419699:5786326"
    modRuntimeOnly "curse.maven:athena-841890:5629396"
    modRuntimeOnly "curse.maven:geckolib-388172:5605709"
    modRuntimeOnly "curse.maven:emi-580555:6205505"
    // modRuntimeOnly "curse.maven:jei-238222:6516055"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = BETA
    modLoaders.add("fabric")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "J8MMsNrL"
        minecraftVersions.add("1.21.1")

        requires("owo-lib")
        requires("architectury-api")

        announcementTitle = "Fabric Version on Modrinth"
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        projectId = "1206582"
        minecraftVersions.add("1.21.1")

        requires("owo-lib")
        requires("architectury-api")

        javaVersions.add(JavaVersion.VERSION_21)
        clientRequired = true
        serverRequired = false

        projectSlug = "oritech"
        announcementTitle = "Fabric Version on CurseForge"
    }
}
